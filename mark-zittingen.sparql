PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sign: <http://mu.semte.ch/vocabularies/ext/signing/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>

CONSTRUCT { GRAPH ?g { ?zitting ext:poisonedZitting "yes" .} } WHERE {

  ?thing ext:poisonedResource "yes" .

  OPTIONAL {
 GRAPH ?g { ?zitting a besluit:Zitting . }
    {
      ?pr ext:publishesBehandeling ?thing .
      ?zitting prov:wasDerivedFrom ?pr .
    }
    UNION
    {
      ?thing dct:subject ?ap .
      ?zitting besluit:behandelt ?ap .
    }
    UNION
    {
      ?zitting prov:wasDerivedFrom ?thing .
    }
    UNION
    {
      ?bvap ?something ?thing .
      ?bvap dct:subject ?ap .
      ?zitting besluit:behandelt ?ap .
    }
    UNION
    {
      ?zitting ext:uittreksel ?thing .
    }
    UNION
    {
      ?pr ext:publishesBesluitenlijst ?thing .
      ?zitting prov:wasDerivedFrom ?pr .
    }
    UNION
    {
      ?zitting ext:besluitenlijst ?thing .
    }
    UNION
    {
      ?zitting besluit:heeftNotulen ?thing .
    }
    UNION
    {
      ?pr ext:publishesNotulen ?thing .
      ?zitting prov:wasDerivedFrom ?pr .
    }
  }
}
